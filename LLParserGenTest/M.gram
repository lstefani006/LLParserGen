%lexer

fun                                   ^FUN
var                                   ^VAR
class                                 ^CLASS
interface                             ^INTERFACE
new                                   ^NEW
namespace                             ^NAMESPACE

get                                   ^GET
set                                   ^SET

if                                    ^IF
else                                  ^ELSE
while                                 ^WHILE
break                                 ^BREAK
continue                              ^CONTINUE
cast                                  ^CAST
return                                ^RETURN

true                                  ^TRUE
false                                 ^FALSE
null                                  ^NULL
this                                  ^THIS
base                                  ^BASE

native                                ^NATIVE
virtual                               ^VIRTUAL

"."									  ^(.)
","									  ^(,)
";"									  ^(;)
":"									  ^(:)
"+"									  ^(+)
"-"									  ^(-)
"*"									  ^(*)
"%"									  ^(%)
"|"									  ^(|)
"!"									  ^(!)
"^"									  ^(^)
"&"									  ^(&)
"/"									  ^(/)
"("									  ^(()
")"									  ^())
"{"									  ^({)
"}"									  ^(})
"["									  ^([)
"]"									  ^(])
"="									  ^(=)
">"									  ^GT
">="								  ^GE
"<"		    						  ^LT
"<="	    						  ^LE
"=="	    						  ^EQ
"!="	    						  ^NE
">>"	    						  ^SHR
"<<"	    						  ^SHL
"&&"	    						  ^ANDAND
"||"	    						  ^OROR

(_|[a-z]|[A-Z])(_|[a-z]|[A-Z]|[0-9])* ^ID
[0-9]+                                ^NUM
(\ |\n|\r|\t)+                        ^()
\/\/.*                                ^()

"/*"                                   
{
	for (;;)
	{
		if (rd.Peek().ch == -1) throw new Exception("EOF in comment");
		if (rd.Read().ch == '*' && rd.Peek().ch == '/')
		{
			rd.Read();
			break;
		}
	}
	rd.SetMatch();
	rd.EndToken(out tk.strRead, out tk.fileName, out tk.line);
	return false;
}
"\""
{
	var sb = new StringBuilder();
	sb.Append('"');
	for (;;)
	{
		int ch = rd.Read().ch;
		if (ch == -1) throw new Exception("EOF in string");
		if (ch == '"')
			break;
		if (ch == '\\')
			ch = rd.Read().ch;
		else
			sb.Append((char)ch);
	}
	sb.Append('"');

	rd.SetMatch();
	rd.EndToken(out tk.strRead, out tk.fileName, out tk.line);
	tk.strRead = sb.ToString();
	tk.token = STR;
	return true;
}
"'"{
	int ch = rd.Read().ch;
	if (ch == -1) throw new Exception("EOF in char");
	if (rd.Read().ch != '\'') throw new Exception("unterminated char");

	rd.SetMatch();
	rd.EndToken(out tk.strRead, out tk.fileName, out tk.line);
	tk.strRead = ((char)ch).ToString();
	tk.token = CHR;
	return true;
}

%parser 
%parser.class MParser
%parser.using System
%parser.using System.Text
%parser.namespace LLParserGenTest

start<DeclList>
	: decl_list_e   ^$1
	;

decl_list_e<DeclList>
	:              ^new DeclList()
	| decl_list    ^$1
	;

decl_list<DeclList>
	: decl          ^new DeclList().Add($1)
	| decl_list decl    ^$1.Add($2)
	;

decl<DeclRoot>
	: decl_cls      ^$1
	| decl_fun      ^$1
	| decl_var      ^$1
	| decl_namespace ^$1
	;

decl_namespace<DeclNamespace>
	: NAMESPACE ID '{' decl_list_e '}'   ^new DeclNamespace($2, $4)
	;

decl_cls<DeclRoot>
	: CLASS ID     base_opz '{' cls_members_e '}' ^new DeclClass($2, $3, $5)
	| INTERFACE ID base_opz '{' cls_members_e '}' ^new DeclInterface($2, $3, $5)
	;

base_opz<RefTypeRootList>
	: ':' baselist           ^$2
	|                        ^new RefTypeRootList();
	;

baselist<RefTypeRootList>
	: type_dot                ^new RefTypeRootList().Add($1)
	| baselist ',' type_dot   ^$1.Add($3)
	;

cls_members_e<DeclList>
	:                   ^new DeclList();
	| cls_members       ^$1
	;

cls_members<DeclList>
	: cls_member               ^new DeclList().Add($1)
	| cls_members cls_member   ^$1.Add($2)
	;

cls_member<DeclRoot>
	: decl_fun               ^$1
	| decl_var               ^$1
	| decl_cls               ^$1
	;

decl_var<DeclRoot>
	: VAR ID ':' type ';'                              ^new DeclVar($2, $4)
	| VAR ID ':' type '{' decl_var_getset_list '}'     ^new DeclProp($2, $4, $6)
	;

decl_var_getset_list<DeclProp>
	: decl_var_getset                      ^new DeclProp(null, $1, null)
	| decl_var_getset decl_var_getset      ^new DeclProp(null, $1, $2)
	;

decl_var_getset<DeclPropGetSet>
	: SET '{' stmtliste '}'                ^new DeclPropGetSet(true, $3)
	| SET ';'                              ^new DeclPropGetSet(true, null)
	| GET '{' stmtliste '}'                ^new DeclPropGetSet(false, $3)
	| GET ';'                              ^new DeclPropGetSet(false, null)
	;

decl_fun<DeclFun>
	: FUN decl_fun_mod ID '(' funargse ')' ret_or_base '{' stmtliste '}'   ^new DeclFun($2, $3, $5, $7, $9, $10)
	| FUN decl_fun_mod ID '(' funargse ')' ret_or_base ';'                 ^new DeclFun($2, $3, $5, $7, null, $8)
	;

decl_fun_mod<TokenAST>
	:           ^null
	| NATIVE    ^$1
	| VIRTUAL   ^$1
	;

ret_or_base<RefTypeRoot_or_Base>
	: ':' BASE '(' e_list_opz ')'       ^new RefTypeRoot_or_Base(new BaseInit($2, $4))
	| ':' THIS '(' e_list_opz ')'       ^new RefTypeRoot_or_Base(new BaseInit($2, $4))
	| ':' type                          ^new RefTypeRoot_or_Base($2)
	|                                   ^null
	;

funargse<FunArgList>
	:                      ^new FunArgList()
	| funargs              ^$1
	;

funargs<FunArgList>
	: ID ':' type                   ^new FunArgList().Add($1, $3)
	| funargs ',' ID ':' type       ^$1.Add($3, $5)
	;

stmt<StmtRoot>
	: IF '(' expr ')' stmt             ^new StmtBlock(new StmtIf($1, $3, new StmtBlock($5)))
	| IF '(' expr ')' stmt *ELSE stmt  ^new StmtBlock(new StmtIf($1, $3, new StmtBlock($5), new StmtBlock($7)))
	| expr ';'                         ^new StmtExpr($2, $1)
	| VAR ID ':' type ';'              ^new StmtVar($1, $2, $4)
	| '{' stmtliste '}'                ^new StmtBlock($1, $2)
	| WHILE '(' expr ')' stmt          ^new StmtBlock(new StmtWhile($1, $3, new StmtBlock($5)))
	| BREAK ';'                        ^new StmtBreak($1);
	| CONTINUE ';'                     ^new StmtContinue($1);
	| RETURN ';'                       ^new StmtReturn($1);
	| RETURN expr ';'                  ^new StmtReturn($1, $2);
	;


stmtliste<StmtList>
	: stmtlist              ^$1
	|                       ^new StmtList();
	;

stmtlist<StmtList>
	: stmtlist stmt          ^$1.Add($2)
	| stmt                   ^new StmtList($1)
	;

expr<ExprRoot> :
	e_ass                ^$1
	;

e_ass<ExprRoot>
	: e_oror '=' e_ass   ^new ExprAss($1, $2, $3)
	| e_oror             ^$1
	;

e_oror<ExprRoot>
	: e_oror OROR e_andand    ^new ExprBinLogical($1, $2, $3)
	| e_andand                ^$1
	;

e_andand<ExprRoot>
	: e_andand ANDAND e_or    ^new ExprBinLogical($1, $2, $3)
	| e_or                    ^$1
	;

e_or<ExprRoot>
	: e_or '|' e_xor    ^new ExprBinGen($1, $2, $3)
	| e_xor             ^$1
	;

e_xor<ExprRoot>
	: e_xor '^' e_and   ^new ExprBinGen($1, $2, $3)
	| e_and             ^$1
	;

e_and<ExprRoot>
	: e_and '&' e_eq   ^new ExprBinGen($1, $2, $3)
	| e_eq             ^$1
	;

e_eq<ExprRoot>
	: e_eq EQ e_lt    ^new ExprBinCompare($1, $2, $3)
	| e_eq NE e_lt    ^new ExprBinCompare($1, $2, $3)
	| e_lt            ^$1
	;

e_lt<ExprRoot>
	: e_lt LT e_sh    ^new ExprBinCompare($1, $2, $3)
	| e_lt LE e_sh    ^new ExprBinCompare($1, $2, $3)
	| e_lt GT e_sh    ^new ExprBinCompare($1, $2, $3)
	| e_lt GE e_sh    ^new ExprBinCompare($1, $2, $3)
	| e_sh            ^$1
	;

e_sh<ExprRoot>
	: e_sh SHL e_add    ^new ExprBinGen($1, $2, $3)
	| e_sh SHR e_add    ^new ExprBinGen($1, $2, $3)
	| e_add             ^$1
	;


e_add<ExprRoot>
	: e_add '+' e_mul  ^new ExprBinGen($1, $2, $3)
	| e_add '-' e_mul  ^new ExprBinGen($1, $2, $3)
	| e_mul            ^$1
	;

e_mul<ExprRoot>
	: e_una            ^$1
	| e_mul '*' e_una  ^new ExprBinGen($1, $2, $3)
	| e_mul '/' e_una  ^new ExprBinGen($1, $2, $3)
	| e_mul '%' e_una  ^new ExprBinGen($1, $2, $3)
	;

e_una<ExprRoot>
	: e_prim           ^$1
	| '-' e_una        ^new ExprNeg($1, $2)
	| '+' e_una        ^new ExprPlus($1, $2)
	;

e_prim<ExprRoot>
	: e_new                          ^$1
	| e_prim_no_array_creation       ^$1
	;

e_prim_no_array_creation<ExprRoot>
	: NUM                               ^new ExprNum($1)
	| STR                               ^new ExprStr($1)
	| CHR                               ^new ExprChr($1)
	| ID                                ^new ExprId($1)
	| THIS                              ^new ExprThis($1)
	| BASE                              ^new ExprThis($1)
	| FALSE                             ^new ExprBool($1, false)
	| TRUE                              ^new ExprBool($1, true)
	| NULL                              ^new ExprNull($1)
	| '(' e_add ')'                     ^$2
	| e_prim_no_array_creation '.' ID                 ^new ExprDot($1, $3)
	| e_prim_no_array_creation '(' e_list_opz ')'     ^new ExprFun($1, $2, $3)
	| e_prim_no_array_creation '[' e_list ']'         ^new ExprArray($1, $2, $3)
	| CAST '(' type ',' expr ')'                      ^new ExprCast($1, $3, $5)
	;

e_new<ExprRoot>
	: NEW type_dot '(' e_list_opz ')'    ^new ExprNewObj($1, $2, $4)
	| NEW type_dot e_new_array           ^new ExprNewArray($1, $2, $3)
	;

e_new_array<ExprRankList>
	: '[' e_list ']'                               ^new ExprRankList().Add($2)
	| e_new_array '[' e_new_array_rank_opz ']'     ^$1.Add($3)
	;

e_new_array_rank_opz<ExprList>
	:                              ^new ExprList().Add(null)
	| e_new_array_rank             ^$1
	;

e_new_array_rank<ExprList>
	: ','                              ^new ExprList().Add(null).Add(null)
	| e_new_array_rank ','             ^$1.Add(null)
	;

e_list_opz<ExprList>
	:                   ^new ExprList()
	| e_list            ^$1
	;

e_list<ExprList>
	: e_ass               ^new ExprList($1)
	| e_list ',' e_ass    ^$1.Add($3)
	;


type<RefTypeRoot>
	: type_dot            ^$1
	| type '[' e_new_array_rank_opz ']'        ^new RefTypeArray($1, $3.Count)
	;

type_dot<RefTypeSimple>
	: ID                  ^new RefTypeSimple($1)
	| type_dot '.' ID     ^$1.Add($3)
	;
